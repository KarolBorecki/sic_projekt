#include "../common.h"
#include <Wire.h>
#include <SPI.h>

const uint32_t MY_NODE_ID = ID_W3;
const int MY_I2C_ADDR = (MY_NODE_ID == ID_W3) ? W3_NR : W4_NR;

const int DATAFRAME_SIZE = sizeof(DataFrame);

volatile bool packetReady = false;
volatile DataFrame bufferFrame;

void setup() {
    Serial.begin(UART_BAUD_RATE);
    
    Serial1.begin(UART_BAUD_RATE);

    Wire.begin(MY_I2C_ADDR);
    Wire.onReceive(receiveEventI2C);
    
    Serial.println("W3 Node Started");
}

void loop() {
    if (packetReady) {
        DataFrame frameToProcess;

        noInterrupts();
        frameToProcess = bufferFrame;
        packetReady = false;
        interrupts();

        processAndForward(frameToProcess);
    }
    
    if (Serial.available() >= DATAFRAME_SIZE) {
        DataFrame usbFrame;
        Serial.readBytes((char*)&usbFrame, DATAFRAME_SIZE);
        processAndForward(usbFrame);
    }
}

void receiveEventI2C(int howMany) {
    if (howMany < DATAFRAME_SIZE) {
        while(Wire.available()) Wire.read();
        return;
    }

    uint8_t* pBuffer = (uint8_t*)&bufferFrame;
    
    for (int i = 0; i < DATAFRAME_SIZE; i++) {
        pBuffer[i] = Wire.read();
    }
    
    packetReady = true;
}

void processAndForward(DataFrame f) {
    f.pathMask |= MY_NODE_ID;

    f.crc = calculateCRC((uint8_t*)&f, DATAFRAME_SIZE - sizeof(uint16_t));

    Serial1.write((uint8_t*)&f, DATAFRAME_SIZE);

    Serial.print("Przekazano ramke od ID: ");
    Serial.println(f.senderID);
}