#include "../common.h"
#include <Arduino.h>
#include "wiring_private.h"

Uart Serial2(&sercom3, 0, 1, SERCOM_RX_PAD_0, UART_TX_PAD_1);

Uart Serial3(&sercom0, 2, 3, SERCOM_RX_PAD_2, UART_TX_PAD_3);

void SERCOM3_Handler() {
    Serial2.IrqHandler();
}

void SERCOM0_Handler() {
    Serial3.IrqHandler();
}

const uint32_t MY_NODE_ID = ID_W0;
unsigned long lastSeen[6];

void setup() {
    Serial.begin(UART_BAUD_RATE);
    
    pinPeripheral(0, PIO_SERCOM); 
    pinPeripheral(1, PIO_SERCOM); 
    Serial2.begin(UART_BAUD_RATE);

    pinPeripheral(2, PIO_SERCOM);
    pinPeripheral(3, PIO_SERCOM);
    Serial3.begin(UART_BAUD_RATE);

    Serial.println("W0 Gateway Started");
}

void loop() {
    DataFrame frame;

    if (trySyncAndReadFrame(Serial2, frame)) {
        processFrame(frame);
    }

    if (trySyncAndReadFrame(Serial3, frame)) {
        processFrame(frame);
    }

    checkSystemHealth();
}

bool trySyncAndReadFrame(Uart &u, DataFrame &f) {
    if (u.available() < sizeof(DataFrame)) {
        return false;
    }

    u.readBytes((uint8_t *)&f, sizeof(DataFrame));

    uint16_t calcCRC = calculateCRC((uint8_t *)&f, sizeof(DataFrame) - sizeof(uint16_t));

    if (calcCRC == f.crc) {
        return true;
    } else {
        Serial.println("CRC Error - Frame rejected");
        return false;
    }
}

void processFrame(DataFrame &frame) {
    frame.pathMask |= MY_NODE_ID;

    if (frame.senderID >= 1 && frame.senderID <= 5) {
        lastSeen[frame.senderID] = millis();
    }

    printDataToUSB(frame);
}

void printDataToUSB(DataFrame f) {
    Serial.print("Dane od S");
    Serial.print(f.senderID);
    Serial.print(" | ADC: ");
    Serial.print(f.measurement);
    Serial.print(" | Sciezka: ");
    for (int i = 5; i >= 0; i--) {
        Serial.print((f.pathMask >> i) & 1);
    }
    Serial.println();
}

void checkSystemHealth() {
    static unsigned long lastCheck = 0;
    unsigned long now = millis();

    if (now - lastCheck > 1000) {
        lastCheck = now;
        for (int i = 1; i <= 5; i++) {
            if (lastSeen[i] != 0 && (now - lastSeen[i] > 10000)) {
                Serial.print("!!! ALARM: Utracono kontakt z S");
                Serial.println(i);
            }
        }
    }
}